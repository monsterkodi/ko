###
 0000000  00000000  000      00000000   0000000  000000000
000       000       000      000       000          000
0000000   0000000   000      0000000   000          000
     000  000       000      000       000          000
0000000   00000000  0000000  00000000   0000000     000
###

class Select

    @: @browser ->

        @rows = []
        @active = null

    files: ->

        # rows = @rows.filter (row) -> row.item.file != '..'
        # rows.map (row) -> row.item.path
        @items().map (item) -> item.path

    items: ->

        rows = @rows.filter (row) -> row.item.file != '..'
        rows.map (row) -> row.item

    freeIndex: ->

        ⮐ -1 if not @active

        index = @active.index()
        while index < @active.column.numRows()-1
            index += 1
            ⮐ index if not @active.column.rows[index].isSelected()

        index = @active.index()
        while index > 0
            index -= 1
            ⮐ index if not @active.column.rows[index].isSelected()
        -1

    clear: ->

        for row in @rows ? []
            row.clearSelected()

        @rows = []
        @active = null

    toggle: row ->

        # return if row == @active

        if row.column != @active?.column
            @row row
            ⮐ 

        if row.isSelected()

            row.clearActive()
            row.clearSelected()
            @rows.splice @rows.indexOf(row), 1
        else
            row.setSelected()
            @active = row
            @rows.push row

    row: (row, activate=true) ->

        ⮐ if not row

        if @active?.column == row.column and activate
            @active?.clearActive()

        @clear()

        @rows = [row]
        @active = row
        row.setSelected()

        if not row.isActive() and activate
            row.activate()

    to: (row, moveActive=false) ->

        ⮐ if not row
        ⮐ if row == @active
        ⮐ if not @active

        if row.column != @active.column
            @row row
            ⮐ 

        if row.index() > @active.index()
            from = @active.index()+1
            to   = row.index()
        else
            from = row.index()
            to   = @active.index()-1

        for index in [from..to]
            r = @active.column.rows[index]
            if not r.isSelected()
                r.setSelected()
                @rows.push r

        if moveActive
            @active?.clearActive()
            @active = row
            @active.setActive()

export Select

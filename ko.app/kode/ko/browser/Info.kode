###
000  000   000  00000000   0000000
000  0000  000  000       000   000
000  000 0 000  000000    000   000
000  000  0000  000       000   000
000  000   000  000        0000000
###

use ../../kxk ▪ pretty ffs elem slash $
use ../tools  ◆ File

# 000  00     00   0000000    0000000   00000000
# 000  000   000  000   000  000        000
# 000  000000000  000000000  000  0000  0000000
# 000  000 0 000  000   000  000   000  000
# 000  000   000  000   000   0000000   00000000

image = file ->

    img = elem 'img' class:'browserImage' src:slash.fileUrl(file)
    imgdiv = elem class:'browserImageDiv' child:img
    cnt = elem class:'browserImageContainer' child:imgdiv
    
    img.addEventListener 'dblclick' -> kore.set 'view|file' file

    table = elem 'table' class:'imageInfoData'
    table.innerHTML = "<tr class='dataRow'><th></th><td></td></tr><tr class='dataRow'><th></th><td></td></tr>"

    img.onload = ○->

        img.style.opacity = '1'

        info =○ ffs.info file
        
        [size, size_unit] = pretty.bytes(info.size).split ' '
        [time, time_unit] = pretty.age(info.modified).split ' '
        table.innerHTML = "<tr class='fileRow'><th colspan=2><div class='fileInfoFile #{slash.ext file}'>#{File.span(file)}</div></th></tr><tr class='dataRow'><th>#{size}</th><td>#{size_unit}</td></tr><tr class='dataRow'><th>#{time}</th><td>#{time_unit}</td></tr>"

    elem class:'browserImageInfo' children: [ elem(class:'imageInfoSpacer'), cnt, table, elem(class:'imageInfoSpacer') ]
    

# 00000000  000  000      00000000
# 000       000  000      000
# 000000    000  000      0000000
# 000       000  000      000
# 000       000  0000000  00000000

file = file ->

    table = elem 'table' class:'fileInfoData'
    table.innerHTML = "<tr class='dataRow'><th></th><td></td></tr><tr class='dataRow'><th></th><td></td></tr>"

    ffs.fileExists(file).then (stat) ○->

        if not stat
            ⮐ error "file #{file} doesn't exist?"

        info =○ ffs.info file
        
        [size, size_unit] = pretty.bytes(info.size).split ' '
        [time, time_unit] = pretty.age(info.modified).split ' '
        table.innerHTML = "<tr class='dataRow'><th>#{size}</th><td>#{size_unit}</td></tr><tr class='dataRow'><th>#{time}</th><td>#{time_unit}</td></tr>"

    info = elem class:'browserFileInfo' children: [
        elem(class:'fileInfoSpacer')
        elem(class:"fileInfoIcon #{slash.ext file} #{File.iconClassName(file)}")
        elem(class:"fileInfoFile #{slash.ext file}" html:File.span(file))
        table
        elem(class:'fileInfoSpacer')
    ]

    if slash.ext(file) in ['html']
        info.addEventListener 'dblclick' -> kore.set 'view|file' file
    
    info

export
    file:   file
    image:  image

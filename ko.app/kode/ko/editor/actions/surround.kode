
#  0000000  000   000  00000000   00000000    0000000   000   000  000   000  0000000  
# 000       000   000  000   000  000   000  000   000  000   000  0000  000  000   000
# 0000000   000   000  0000000    0000000    000   000  000   000  000 0 000  000   000
#      000  000   000  000   000  000   000  000   000  000   000  000  0000  000   000
# 0000000    0000000   000   000  000   000   0000000    0000000   000   000  0000000  

use ../../../kxk ▪ kstr isEqual uniqEqual reversed

export

    initSurround: ->
        
        @surroundStack = []

        @surroundPairs = 
            '#': ['#{', '}'] # <- this has to come
            '{': ['{', '}']  # <- before that
            '}': ['{', '}']
            '[': ['[', ']']
            ']': ['[', ']']
            '(': ['(', ')']
            ')': ['(', ')']
            '<': ['<', '>']
            '>': ['<', '>']
            "'": ["'", "'"]
            '"': ['"', '"']
            '*': ['*', '*']                    
        
        @surroundCharacters = "{}[]()\"'".split ''
        
        switch @fileType
            when 'html'   then @surroundCharacters = @surroundCharacters.concat ['<','>']
            when 'coffee', 'kode' then @surroundCharacters.push '#'
            when 'md'     
                @surroundCharacters = @surroundCharacters.concat ['*','<', '`']
                @surroundPairs['<'] = ['<!--', '-->']
                @surroundPairs['`'] = ['`', '`']
                
    isUnbalancedSurroundCharacter: (ch) ->
        
        return false if ch in ["#"]
        [cl,cr] = @surroundPairs[ch]
        return false if cl.length > 1
        for cursor in @cursors()
            count = 0
            for c in @line(cursor[1])
                if c == cl
                    count += 1
                else if c == cr
                    count -= 1
            if ((cl == cr) and (count % 2)) or ((cl != cr) and count)
                return true
        return false
    
    selectionContainsOnlyQuotes: ->
        
        for c in @textOfSelection()
            continue if c == '\n'
            if c not in ['"', "'"]
                return false
        true
    
    insertTripleQuotes: ->
        
        return false if @numCursors() > 1
        return false if @numSelections()
        p = @cursorPos()
        [before, after] = @splitStateLineAtPos @do, p
        return false if not before.endsWith '""'
        return false if before.length > 2 and before[before.length-3] == '"'
        return false if after.startsWith '"'
        @do.start()
        @do.change p[1], before + '""""' + after
        @do.setCursors [[p[0]+1, p[1]]]
        @do.end()
        true
    
    # 000  000   000   0000000  00000000  00000000   000000000  
    # 000  0000  000  000       000       000   000     000     
    # 000  000 0 000  0000000   0000000   0000000       000     
    # 000  000  0000       000  000       000   000     000     
    # 000  000   000  0000000   00000000  000   000     000     
    
    insertSurroundCharacter: (ch) ->

        if ch == '"' and @fileType in ['coffee' 'kode'] and @insertTripleQuotes()
            return true

        if @isUnbalancedSurroundCharacter ch
            return false 
        
        if @numSelections() and ch in ['"' "'"] and @selectionContainsOnlyQuotes()
            return false
        
        newCursors = @do.cursors()
        
        if @surroundStack.length
            if last(@surroundStack)[1] == ch
                for c in newCursors
                    if @do.line(c[1])[c[0]] != ch
                        @surroundStack = []
                        break
                if @surroundStack.length and last(@surroundStack)[1] == ch
                    @do.start()
                    @selectNone()
                    @deleteForward()
                    @do.end()
                    @surroundStack.pop()
                    return false 
        
        if ch == '#' and @fileType in ['coffee' 'kode'] # check if any cursor or selection is inside a string
            found = false
            for s in @do.selections()
                if @isRangeInString s
                    found = true
                    break
                    
            if not found
                for c in newCursors
                    if @isRangeInString rangeForPos c
                        found = true
                        break
            ⮐ false if not found
            
        if ch == "'" and not @numSelections() # check if any alpabetical character is before any cursor
            for c in newCursors
                ⮐ false if c[0] > 0 and /[A-Za-z]/.test @do.line(c[1])[c[0]-1] 
        
        @do.start()
        if @do.numSelections() == 0
            newSelections = rangesFromPositions newCursors
        else
            newSelections = @do.selections()
            
        [cl,cr] = @surroundPairs[ch]
            
        @surroundStack.push [cl,cr]

        for ns in reversed newSelections
                                    
            if cl == '#{' # convert single string to double string
                if sr = @rangeOfStringSurroundingRange ns
                    if @do.line(sr[0])[sr[1][0]] == "'"
                        @do.change ns[0], kstr.splice @do.line(ns[0]), sr[1][0], 1, '"'
                    if @do.line(sr[0])[sr[1][1]-1] == "'"
                        @do.change ns[0], kstr.splice @do.line(ns[0]), sr[1][1]-1, 1, '"'
                        
            else if @fileType in ['coffee' 'kode'] and cl == '(' and lengthOfRange(ns) > 0 # remove space after callee
                [before, after] = @splitStateLineAtPos @do, rangeStartPos ns
                trimmed = before.trimRight()
                beforeGood = /\w$/.test(trimmed) and not /(if|when|in|and|or|is|not|else|return)$/.test trimmed
                afterGood = after.trim().length and not after.startsWith ' '
                if beforeGood and afterGood
                    spaces = before.length-trimmed.length
                    @do.change ns[0], kstr.splice @do.line(ns[0]), trimmed.length, spaces
                    
                    for c in positionsAfterLineColInPositions ns[0], ns[1][0]-1, newCursors
                        c[0] -= spaces
                    ns[1][0] -= spaces
                    ns[1][1] -= spaces
                    
            @do.change ns[0], kstr.splice @do.line(ns[0]), ns[1][1], 0, cr
            @do.change ns[0], kstr.splice @do.line(ns[0]), ns[1][0], 0, cl
            
            for c in positionsAfterLineColInPositions ns[0], ns[1][0]-1, newCursors
                c[0] += cl.length
                
            for os in rangesAfterLineColInRanges ns[0], ns[1][1]-1, newSelections
                os[1][0] += cr.length
                os[1][1] += cr.length
                
            for os in rangesAfterLineColInRanges ns[0], ns[1][0]-1, newSelections
                os[1][0] += cl.length
                os[1][1] += cl.length
            
            for c in positionsAfterLineColInPositions ns[0], ns[1][1], newCursors
                c[0] += cr.length
                
        @do.select rangesNotEmptyInRanges newSelections
        @do.setCursors newCursors
        @do.end()
        return true

    # 0000000    00000000  000      00000000  000000000  00000000  
    # 000   000  000       000      000          000     000       
    # 000   000  0000000   000      0000000      000     0000000   
    # 000   000  000       000      000          000     000       
    # 0000000    00000000  0000000  00000000     000     00000000  
    
    deleteEmptySurrounds: ->
        
        return if empty @surroundPairs
            
        cs = @do.cursors()

        pairs = uniqEqual Object.values @surroundPairs

        openClosePairs = []
        
        for c in cs
            numPairs = openClosePairs.length
            # check if all cursors are inside of empty surround pairs
            for [so, sc] in pairs
                before = @do.line(c[1]).slice c[0]-so.length, c[0]
                after  = @do.line(c[1]).slice c[0], c[0]+sc.length
                if so == before and sc == after
                    openClosePairs.push [so,sc]
                    break
            if numPairs == openClosePairs.length
                return false 

        if cs.length != openClosePairs.length
            return false 
            
        # all cursors in empty surround -> remove both surrounds
        
        uniquePairs = uniqEqual openClosePairs
        for c in cs
            [so,sc] = openClosePairs.shift()
  
            @do.change c[1], kstr.splice @do.line(c[1]), c[0]-so.length, so.length+sc.length
            for nc in positionsAfterLineColInPositions c[1], c[0], cs
                nc[0] -= sc.length + so.length 
            c[0] -= so.length
        
        if @surroundStack.length # pop or clean surround stack
            if uniquePairs.length == 1 and isEqual uniquePairs[0], last @surroundStack 
                @surroundStack.pop()
            else
                @surroundStack = []
                
        @do.setCursors cs
        
        true    
        
    # 000   000  000   0000000   000   000  000      000   0000000   000   000  000000000   0000000  
    # 000   000  000  000        000   000  000      000  000        000   000     000     000       
    # 000000000  000  000  0000  000000000  000      000  000  0000  000000000     000     0000000   
    # 000   000  000  000   000  000   000  000      000  000   000  000   000     000          000  
    # 000   000  000   0000000   000   000  0000000  000   0000000   000   000     000     0000000   
    
    highlightsSurroundingCursor: ->
        
        if @numHighlights() % 2 == 0
            hs = @highlights()
            sortRanges hs
            if @numHighlights() == 2
                return hs
            else if @numHighlights() == 4
                if areSameRanges [hs[1], hs[2]], @selections()
                    return [hs[0], hs[3]]
                else
                    return [hs[1], hs[2]]


###
000   000   0000000   000   000  000   0000000    0000000   000000000  00000000
0000  000  000   000  000   000  000  000        000   000     000     000
000 0 000  000000000   000 000   000  000  0000  000000000     000     0000000
000  0000  000   000     000     000  000   000  000   000     000     000
000   000  000   000      0      000   0000000   000   000     000     00000000
###

{ _, clamp, filter, post, prefs, slash } = require 'kxk'

class Navigate

    @: (@main) ->

        return if not @main? # not very obvious: this is instantiated in main and window processes

        post.onGet 'navigate' @onGet
        post.on 'navigate' @navigate
        @filePositions = prefs.get 'filePositions' []
        @currentIndex = -1
        @navigating = false

    # 00     00   0000000   000  000   000
    # 000   000  000   000  000  0000  000
    # 000000000  000000000  000  000 0 000
    # 000 0 000  000   000  000  000  0000
    # 000   000  000   000  000  000   000

    onGet: (key) => @[key]

    addToHistory: (file, pos) ->

        return if not @main
        return if not file?
        
        pos ?= [0,0]
        
        if not pos[0] and not pos[1] and @filePositions.length
            for i in [@filePositions.length-1..0]
                fp = @filePositions[i]
                if slash.samePath fp.file, file
                    pos = fp.pos
                    break
            
        _.pullAllWith @filePositions, [file:file, pos:pos], (a,b) -> 
            slash.samePath(a.file, b.file) and (a.pos[1] == b.pos[1] or a.pos[1] <= 1)

        filePos = slash.tilde slash.joinFilePos file, pos
        
        if @filePositions[-1]?.file == file and @filePositions[-1]?.pos[1] == pos[1]-1
            @filePositions.pop()
        
        @filePositions.push
            file:   file
            pos:    pos
            line:   pos[1]+1
            column: pos[0]
            name:   filePos
            text:   slash.file filePos

        while @filePositions.length > prefs.get 'navigateHistoryLength' 100
            @filePositions.shift()
            
        prefs.set 'filePositions' @filePositions

    navigate: (opt) =>

        switch opt.action

            when 'clear'
                @filePositions = []
                @currentIndex = -1

            when 'backward'
                return if not @filePositions.length
                @currentIndex = clamp 0, Math.max(0,@filePositions.length-2), @currentIndex-1
                @navigating = true
                @loadFilePos @filePositions[@currentIndex], opt

            when 'forward'
                return if not @filePositions.length
                @currentIndex = clamp 0, @filePositions.length-1, @currentIndex+1
                @navigating = true
                @loadFilePos @filePositions[@currentIndex], opt
                
            when 'delFilePos'

                opt.item.line ?= opt.item.pos?[1]+1

                @filePositions = filter @filePositions, (f) -> f.file != opt.item.file or f.line != opt.item.line
                @currentIndex = clamp 0, @filePositions.length-1, @currentIndex
                post.toWins 'navigateHistoryChanged' @filePositions, @currentIndex
                
            when 'addFilePos'

                return if not opt?.file?.length

                @addToHistory opt.oldFile, opt.oldPos

                hasFile = _.find @filePositions, (v) -> v.file == opt.file

                if not @navigating or not hasFile or opt?.for in ['edit' 'goto']

                    @navigating = false if opt?.for in ['edit' 'goto']

                    @addToHistory opt.file, opt.pos

                    @currentIndex = @filePositions.length-1

                    if opt?.for == 'goto'
                        post.toWins 'navigateHistoryChanged' @filePositions, @currentIndex
                        @loadFilePos @filePositions[@currentIndex], opt
                    else
                        @currentIndex = @filePositions.length
                        post.toWins 'navigateHistoryChanged' @filePositions, @currentIndex

    loadFilePos: (filePos, opt) ->

        if opt?.newWindow
            post.toMain 'newWindowWithFile' "#{filePos.file}:#{filePos.pos[1]+1}:#{filePos.pos[0]}"
        else
            error 'no winID?' if not opt?.winID?
            post.toWin opt.winID, 'loadFile' "#{filePos.file}:#{filePos.pos[1]+1}:#{filePos.pos[0]}"

        post.toWins 'navigateIndexChanged' @currentIndex, @filePositions[@currentIndex]

        filePos

    # 000   000  000  000   000
    # 000 0 000  000  0000  000
    # 000000000  000  000 0 000
    # 000   000  000  000  0000
    # 00     00  000  000   000

    # these are called in window process

    delFilePos: (item) ->
        post.toMain 'navigate' action:'delFilePos' winID: window.winID, item:item

    addFilePos: (opt) -> # called on editing
        opt.action = 'addFilePos'
        opt.for = 'edit'
        post.toMain 'navigate' opt

    gotoFilePos: (opt) -> # called on jumpTo
        opt.action = 'addFilePos'
        opt.for = 'goto'
        post.toMain 'navigate' opt

    backward: -> post.toMain 'navigate' action: 'backward' winID: window.winID
    forward:  -> post.toMain 'navigate' action: 'forward'  winID: window.winID
    clear:    -> post.toMain 'navigate' action: 'clear'    winID: window.winID

module.exports = Navigate

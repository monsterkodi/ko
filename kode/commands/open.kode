###
 0000000   00000000   00000000  000   000
000   000  000   000  000       0000  000
000   000  00000000   0000000   000 0 000
000   000  000        000       000  0000
 0000000   000        00000000  000   000
###

kxk = require 'kxk'
{ _, post, slash } = kxk
  
Projects  = require '../tools/projects'
File      = require '../tools/file'
Command   = require '../commandline/command'
render    = require '../editor/render'
syntax    = require '../editor/syntax'
fuzzy     = require 'fuzzy'
                 
relative = (rel, to) ->
    
    r = slash.relative rel, to

    if r.startsWith '../../' 
        tilde = slash.tilde rel
        if tilde.length < r.length
            r = tilde
    if rel.length < r.length    
        r = rel
    r    

function Open extends Command

    @: (commandline) ->
        
        super commandline
        
        post.on 'file', @onFile
        
        @names    = ["open", "new window"]
        @files    = []
        @file     = null
        @dir      = null
        @pkg      = null
        @selected = 0
          
    onFile: (file) =>
        
        if @isActive() 
            if empty file
                @setText ''
            else if @getText() != slash.file file
                @setText slash.tilde file
                
    #  0000000  000   000   0000000   000   000   0000000   00000000  0000000  
    # 000       000   000  000   000  0000  000  000        000       000   000
    # 000       000000000  000000000  000 0 000  000  0000  0000000   000   000
    # 000       000   000  000   000  000  0000  000   000  000       000   000
    #  0000000  000   000  000   000  000   000   0000000   00000000  0000000  

    changed: (command) ->
        
        command = command.trim()

        [file, pos] = slash.splitFilePos command ? @getText().trim()

        items = @listItems currentText:command, maxItems:10000
        
        if command.length
            
            fuzzied = fuzzy.filter slash.basename(file), items, extract: (o) -> o.text            
            items = (f.original for f in fuzzied)
            items.sort (a,b) -> b.weight - a.weight
                    
        if items.length
            @showItems items.slice 0 300
            @select 0
            @positionList()
        else
            @hideList()

    #  0000000   0000000   00     00  00000000   000      00000000  000000000  00000000
    # 000       000   000  000   000  000   000  000      000          000     000     
    # 000       000   000  000000000  00000000   000      0000000      000     0000000 
    # 000       000   000  000 0 000  000        000      000          000     000     
    #  0000000   0000000   000   000  000        0000000  00000000     000     00000000

    complete: ->

        if @commandList? and @commandList.line(@selected).startsWith(slash.basename @getText()) and not @getText().trim().endsWith('/')
            @setText slash.join(slash.dir(@getText()), @commandList.line(@selected))
            if slash.dirExists @getText()
                @setText @getText() + '/'
                @changed @getText()
            true
        else if not @getText().trim().endsWith('/') and slash.dirExists @getText()
            @setText @getText() + '/'
            @changed @getText()
            true            
        else
            projects = post.get 'indexer', 'projects'
            for p in Object.keys(projects).sort()
                if p.startsWith @getText()
                    pdir = projects[p].dir
                    pdir = slash.join(pdir, 'coffee') if slash.dirExists slash.join pdir, 'coffee'
                    @setText pdir + '/'
                    @changed @getText()
                    return true
            super()
    
    # 000   000  00000000  000   0000000   000   000  000000000
    # 000 0 000  000       000  000        000   000     000   
    # 000000000  0000000   000  000  0000  000000000     000   
    # 000   000  000       000  000   000  000   000     000   
    # 00     00  00000000  000   0000000   000   000     000   

    weight: (item, opt) =>            
                
        return item.bonus if item.bonus?
        
        f = item.file
        r = item.text
        b = slash.file f
        n = slash.base f
                
        relBonus = 0
        nameBonus = 0
        if opt.currentText?.length
            relBonus  = r.startsWith(opt.currentText) and 65535 * (opt.currentText.length/r.length) or 0 
            nameBonus = n.startsWith(opt.currentText) and 2184  * (opt.currentText.length/n.length) or 0
           
        extensionBonus = switch slash.ext b
            'coffee' 'kode'    ➜ 1000
            'cpp' 'hpp' 'h'    ➜ 90
            'md' 'styl' 'pug'  ➜ 50
            'noon'             ➜ 25
            'js' 'json' 'html' ➜ -10
                               ➜ 0 
        
        if @file and slash.ext(@file) == slash.ext b
            extensionBonus += 1000
        
        lengthPenalty = slash.dir(f).length
            
        updirPenalty   = r.split('../').length * 819
        
        if f.startsWith @dir
            localBonus = Math.max 0, (5-r.split('/').length) * 4095
        else
            localBonus = Math.max 0, (5-r.split('../').length) * 819
        
        item.weight = localBonus + relBonus + nameBonus + extensionBonus - lengthPenalty - updirPenalty
            
    weightedItems: (items, opt) -> 
        
        items.sort (a,b) => @weight(b, opt) - @weight(a, opt)
        items
    
    # 000      000   0000000  000000000
    # 000      000  000          000   
    # 000      000  0000000      000   
    # 000      000       000     000   
    # 0000000  000  0000000      000   
        
    listItems: (opt) ->
        
        opt ?= {}
        opt.maxItems ?= 200
        opt.flat ?= true
        
        iconSpan = (file) ->
            
            className = File.iconClassName file
            "<span class='#{className} openFileIcon'/>"
        
        items = []
        
        @lastFileIndex = 0
        
        @dir = slash.resolve '~' if not @dir?
        
        if @history? and not opt.currentText and @history.length > 1
            
            f = @history[@history.length-2]
            item = Object.create null
            item.text = relative f, @dir
            item.line = iconSpan f
            item.file = f
            item.bonus = 1048575
            items.push item
            @lastFileIndex = 0
                
        if valid @files
            for file in @files
                
                rel = relative file, @dir
                            
                if rel.length
                    item = Object.create null
                    item.line = iconSpan file
                    item.text = rel
                    item.file = file
                    items.push item

        items = @weightedItems items, opt
        items = _.uniqBy items, (o) -> o.text
        
        items.slice 0, opt.maxItems
    
    # 000   000  000   0000000  000000000   0000000   00000000   000   000  
    # 000   000  000  000          000     000   000  000   000   000 000   
    # 000000000  000  0000000      000     000   000  0000000      00000    
    # 000   000  000       000     000     000   000  000   000     000     
    # 000   000  000  0000000      000      0000000   000   000     000     
    
    showHistory: ->

        if @history.length > 1 and @selected <= 0
            items = []
            bonus = 1048575
            for f in @history
                item = Object.create null
                item.text = relative f, @dir
                item.file = f
                item.bonus = bonus
                items.push item
                bonus -= 1 
            items.pop()
            @showItems items
            @select items.length-1
            @setAndSelectText items[@selected].text
        else
            'unhandled'

    showFirst: ->
        
        if @commandList and @selected == @commandList.meta?.metas?.length - 1
            @showItems @listItems()
            @select 0
        else
            'unhandled'
                
    #  0000000   0000000   000   000   0000000  00000000  000    
    # 000       000   000  0000  000  000       000       000    
    # 000       000000000  000 0 000  000       0000000   000    
    # 000       000   000  000  0000  000       000       000    
    #  0000000  000   000  000   000   0000000  00000000  0000000
    
    cancel: (name) ->
        
        if name == @names[0] # command+p command+p to open previous file
            if @commandList? and @lastFileIndex == @selected
                return @execute()
                
        super name
    
    #  0000000  000000000   0000000   00000000   000000000
    # 000          000     000   000  000   000     000   
    # 0000000      000     000000000  0000000       000   
    #      000     000     000   000  000   000     000   
    # 0000000      000     000   000  000   000     000   
        
    start: (name) -> 
        
        @setName name
        
        if @commandline.lastFocus == 'commandline-editor' == window.lastFocus
            
            @file = window.editor.currentFile
            if dir = slash.resolve @commandline.text()
                @dir = dir
            else
                @dir = slash.dir(@file) ? process.cwd()
        
        else if @commandline.lastFocus == 'shelf' or @commandline.lastFocus.startsWith 'FileBrowser'
            
            item = window.filebrowser.lastUsedColumn().parent
            
            switch item.type
                when 'dir'
                    @file = window.editor.currentFile
                    @dir  = item.file
                when 'file'
                    @file = item.file
                    @dir  = slash.dir @file
                    
        else if window.editor.currentFile?
            
            @file = window.editor.currentFile
            @dir  = slash.dir @file
            
        else 
            
            @file = null
            @dir  = process.cwd()
            
        @files = Projects.files @dir
        
        @loadState()
        @showList()
        @showItems @listItems()
        @grabFocus()
        @select 0  
        
        text:   @commandList.line @selected
        select: true
                
    # 00000000  000   000  00000000   0000000  000   000  000000000  00000000
    # 000        000 000   000       000       000   000     000     000     
    # 0000000     00000    0000000   000       000   000     000     0000000 
    # 000        000 000   000       000       000   000     000     000     
    # 00000000  000   000  00000000   0000000   0000000      000     00000000
        
    execute: (command) ->
        
        if @selected < 0 then return status:'failed'
            
        path = @commandList?.line @selected
        
        @hideList()

        if valid path
            
            [file, pos] = slash.splitFilePos command
            
            file = @resolvedPath path
            file = slash.joinFilePos file, pos
            
            if @name == 'new window'
                post.toMain 'newWindowWithFile' file
            else
                post.emit 'jumpToFile' type:'file' file:file
                        
            super file
                
            text:   file
            focus:  'editor'
            show:   'editor'
            status: 'ok'
        else
            status: 'failed'
              
    # 00000000   00000000   0000000   0000000   000      000   000  00000000  0000000  
    # 000   000  000       000       000   000  000      000   000  000       000   000
    # 0000000    0000000   0000000   000   000  000       000 000   0000000   000   000
    # 000   000  000            000  000   000  000         000     000       000   000
    # 000   000  00000000  0000000    0000000   0000000      0      00000000  0000000  
    
    resolvedPath: (p, parent=@dir) ->
        
        return (parent ? slash.resolve '~') if not p?
        if p[0] in ['~', '/'] or p[1] == ':'
            slash.resolve p
        else
            slash.resolve slash.join parent, p

    # 000   000  00000000  000   000
    # 000  000   000        000 000 
    # 0000000    0000000     00000  
    # 000  000   000          000   
    # 000   000  00000000     000   
    
    handleModKeyComboEvent: (mod, key, combo, event) -> 
        
        switch combo
            when 'up'   then return @showHistory()
            when 'down' then return @showFirst()
        super mod, key, combo, event

module.exports = Open
